apply plugin: 'com.android.application'
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.4+"
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.karlo.learningapplication"
        minSdkVersion 21
        targetSdkVersion 27
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.example.karlo.sstconference.TestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    testOptions {
        animationsDisabled = true
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        abortOnError false
        disable 'GradleCompatible','InvalidPackage'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'

    testImplementation 'junit:junit:4.12'
    // -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // -- Test Runner
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    // -- UI testing with Espresso
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    // -- UI testing with UI Automator
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'

    androidTestImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation "android.arch.core:core-testing:1.1.1"

    // -- Dagger 2

    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.13'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.13'

    // -- Mockito

    testImplementation 'org.mockito:mockito-core:2.8.9'
    androidTestImplementation 'org.mockito:mockito-android:2.7.22'

    // -- Retrofit --
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // -- okHttp
    implementation 'com.squareup.okhttp:okhttp:2.7.2'
    implementation('com.squareup.okhttp3:logging-interceptor:3.9.1')

    // -- Gson --
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // -- ButterKnife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // -- Firebase
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-database:16.0.1'
    implementation 'com.google.firebase:firebase-storage:16.0.1'
    implementation 'com.google.firebase:firebase-auth:16.0.1'
    implementation 'com.firebaseui:firebase-ui-storage:0.6.0'

    // -- EasyPrefs
    implementation 'com.github.globulus.easyprefs:easyprefs:84010d9299'

    // -- Google Signup
    implementation 'com.google.android.gms:play-services-auth:15.0.1'

    // -- RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // -- Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    // -- Dagger 2
    implementation 'com.google.dagger:dagger:2.13'
    implementation 'com.google.dagger:dagger-android:2.13'
    implementation 'com.google.dagger:dagger-android-support:2.13'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.13'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.13'

    // -- Architecture Components
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.persistence.room:runtime:1.1.0"
    implementation 'android.arch.persistence.room:rxjava2:1.1.0'
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.0"

    // -- Stream
    implementation 'com.annimon:stream:1.2.0'

    // -- Google Maps
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/coverage.ec'
    ])
}

apply plugin: 'com.google.gms.google-services'
